plugins {
    id 'org.springframework.boot' version '2.3.12.RELEASE'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.google.cloud.tools.jib' version '3.2.1'
    id 'java'
}

group = 'my'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "Hoxton.SR12")
    set('resilience4jVersion', "1.7.0") // 책과 동일하게 1.5.0으로 지정했으나 resilience4j-spring-boot2는 1.5.0으로, resilice4j는 최신버전인 1.7.0으로 주입되는 이슈가 있어 최신 버전으로 고정함.
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation "io.github.resilience4j:resilience4j-spring-boot2:${resilience4jVersion}"
    implementation "io.github.resilience4j:resilience4j-all:${resilience4jVersion}"
    implementation "io.micrometer:micrometer-registry-prometheus" // /actuator/prometheus 에 p8s 메트릭을 노출해준다.

    runtimeOnly 'org.postgresql:postgresql'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

// usage: ./gradlew jibDockerBuild
jib {
    from {
        image = "openjdk:11-slim"
        platforms { // Apple M1을 위해 지정
            platform {
                architecture = "arm64"
                os = "linux"
            }
        }
    }
    to {
        image = "license-server"
        tags = ["latest"]
    }
    container {
        creationTime = "USE_CURRENT_TIMESTAMP"
        jvmFlags = [
//                "-Xms256m",
//                "-Xmx512m"
        ]
//        ports = ['8080']  // port를 지정해 주지 않아도 정상 동작함
    }
}
